	Name     U180 logic ;
PartNo   x180 ;
Date     2025-11-14 ;
Revision 01 ;
Designer MiDi ;
Company   ;
Assembly  ;
Location  ;
Device   f1502isptqfp44 ;

/* the fitter has limitation (making jed file): no spaces and max 128 characters in path - otherwise no jed is made and no error thrown */

/* Convert JED file to SVF with ATMISP program (https://ww1.microchip.com/downloads/en/DeviceDoc/ATMISP7.zip): */
/* File: new, Device: ATF1502AS, JTAG Instruction: Program/Verify, Jedec file: this file, OK, Checkbox: Write SVF file, SVF File Name: Set output filename, RUN */

/* Programming with a JTAG programmer (e.g. FT232HQ) and OpenOCD https://github.com/openocd-org/openocd/releases - download & unpack the latest *mingw32.tar.gz */
/* For FT232HQ under Windows replace driver with WinUSB with tool Zadig (https://zadig.akeo.ie/) */
/* Connection FT232HQ - x180 JTAG: 3V - +5, Gnd - GND, D0 - CK, D1 - DI, D2 - DO, D3 - MS
/* Go to the path of OpenOCD and run (replace *PATH* with the path to the x180.svf file): */
/* bin\openocd.exe -f share\openocd\scripts\interface\ftdi\um232h.cfg -c "adapter speed 400" -c "transport select jtag" -c "jtag newtap ATF1502AS tap -irlen 3 -expected-id 0x0150203f" -c init -c "svf *PATH*\x180.svf progress" -c "sleep 200" -c shutdown */
/* This operation takes ~20s and the progress is shown, otherwise check error message of OpenOCD */

/* from https://www.hackup.net/2020/01/erasing-and-programming-the-atf1504-cpld/ & https://github.com/peterzieba/5Vpld */

PROPERTY ATMEL { TDI_PULLUP = ON };         /* TDI pull up                     */
PROPERTY ATMEL { TMS_PULLUP = ON };         /* TMD pull up                     */
/* PROPERTY ATMEL { PIN_KEEP = OFF };          /* all outputs w/o pin keeper      */
/* PROPERTY ATMEL { OUTPUT_FAST = ON };        /* all outputs use fast slew rate  */
/* PROPERTY ATMEL {OUTPUT_FAST=O4,O5};         /* O4,O5 outputs use fast slew rate*/
/* PROPERTY ATMEL { FASTINPUT = ON }; */

/* *************** INPUT PINS ******************** */
PIN 23       = ENR;                         /*                                 */
PIN 27       = ENS;                         /*                                 */
PIN 33       = SLA;                         /*                                 */
PIN 31       = SLB;                         /*                                 */
PIN 25       = RUR;                         /*                                 */
PIN 30       = RUZ;                         /*                                 */
PIN 28       = RUS;                         /*                                 */
PIN 37       = CLK;                         /* GCLK1                           */ 

/* *************** OUTPUT PINS ******************** */
PIN 34       = RUSN3;                       /* S0:  13 AC_AD                   */
PIN 14       = RUSN2;                       /* S1:  14 AGND                    */
PIN 35       = RUSN1;                       /* S2:  18 DC_AD                   */
PIN 15       = LADSN;                       /* S3:  15 rundown ladder sum node */
PIN 21       = NS2;                         /* S4:  23 negative RU speed ref2  */
PIN 22       = NR2;                         /* S5:  22 negative RU resol ref2  */
PIN 12       = NS1;                         /* S6:  21 negative RU speed ref1  */
PIN 13       = NR1;                         /* S7:  27 negative RU resol ref1  */
PIN 19       = PS2;                         /* S8:  30 positive RU speed ref2  */
PIN 18       = PR2;                         /* S9:  25 positive RU resol ref2  */
PIN 11       = PS1;                         /* S10: 28 positive RU speed ref1  */
PIN 10       = PR1;                         /* S11: 31 positive RU resol ref1  */
PIN 20       = ZERO;                        /* S17: 20 zero integrator         */
/* PIN        = NENR;                       /* inverted ENR - not connected    */
/* PIN        = NENS;                       /* inverted ENS - not connected    */


/* *************** CLOCK NET D-FF **************** */
RUSN3.CK = !CLK;
RUSN2.CK = !CLK;
RUSN1.CK = !CLK;
LADSN.CK = !CLK;
NS2.CK   = !CLK;
NR2.CK   = !CLK;
NS1.CK   = !CLK;
NR1.CK   = !CLK;
PS2.CK   = !CLK;
PR2.CK   = !CLK;
PS1.CK   = !CLK;
PR1.CK   = !CLK;
ZERO.CK  = !CLK;

/* *************** DECODER LOGIC ***************** */
RUSN3.D  = !RUR & !RUZ & RUS;
RUSN2.D  = !RUR & RUZ & !RUS;
RUSN1.D  = RUR & !RUZ & !RUS;
LADSN.D  = (RUR & RUZ) # (RUR & RUS) # (RUZ & RUS); /* simplified from (RUR & RUZ & RUS) # (RUR & RUZ & !RUS) # (RUR & !RUZ & RUS) # (!RUR & RUZ & RUS) */
NS2.D    = ENS & !SLB;
NR2.D    = ENR & !SLB;
NS1.D    = ENS & !SLA;
NR1.D    = ENR & !SLA;
PS2.D    = ENS & SLB;
PR2.D    = ENR & SLB;
PS1.D    = ENS & SLA;
PR1.D    = ENR & SLA;
ZERO.D   = !ENR & !ENS & !RUR & !RUZ & !RUS;